function(xpStringIndentAppend appendTo indent str)
  if(${indent} GREATER "0")
    foreach(i RANGE 1 ${indent})
      set(spaces "${spaces}  ") # 2 spaces per indent level
    endforeach()
    set(${appendTo} "${${appendTo}}${spaces}${str}\n" PARENT_SCOPE)
  else()
    set(${appendTo} "${${appendTo}}${str}\n" PARENT_SCOPE)
  endif()
endfunction()

function(xpCpackWixMsm mergeModuleIds appendTo)
  set(PFX86 "ProgramFiles(x86)")
  file(TO_CMAKE_PATH "$ENV{${PFX86}}/Common Files/Merge Modules" msmDir)
  xpGetCompilerPrefix(compiler)
  string(TOUPPER ${compiler} COMPILER)
  if(MSVC AND ${CMAKE_GENERATOR} MATCHES "Win64$")
    set(ARCH x64)
  elseif(MSVC)
    set(ARCH x86)
  endif()
  set(xml "\n")
  xpStringIndentAppend(xml 4 "<DirectoryRef Id='TARGETDIR'>")
  foreach(id ${mergeModuleIds})
    set(msmPath ${msmDir}/Microsoft_${COMPILER}_${id}_${ARCH}.msm)
    if(EXISTS ${msmPath})
      xpStringIndentAppend(xml 5 "<Merge Id='${id}' SourceFile='${msmPath}' DiskId='1' Language='0'/>")
    else()
      message(AUTHOR_WARNING "merge module ${id} not found: ${msmPath}")
    endif()
  endforeach()
  xpStringIndentAppend(xml 4 "</DirectoryRef>")
  xpStringIndentAppend(xml 4 "<Feature Id='MergeModules' Title='Merge Modules' AllowAdvertise='no' Display='hidden' Level='1'>")
  foreach(id ${mergeModuleIds})
    xpStringIndentAppend(xml 5 "<MergeRef Id='${id}'/>")
  endforeach()
  xpStringIndentAppend(xml 4 "</Feature>")
  set(${appendTo} "${${appendTo}}${xml}" PARENT_SCOPE)
endfunction()

function(xpCpackWixDisableRM appendTo)
  set(xml "\n")
  xpStringIndentAppend(xml 4 "<Property Id='MSIRESTARTMANAGERCONTROL' Value='Disable'/>")
  set(${appendTo} "${${appendTo}}${xml}" PARENT_SCOPE)
endfunction()

function(xpCpackWixUpgradeCondition appendTo guid id prod)
  string(TOUPPER ${id} ID)
  set(xml "\n")
  xpStringIndentAppend(xml 4 "<Upgrade Id='${guid}'>")
  xpStringIndentAppend(xml 5 "<UpgradeVersion OnlyDetect='yes' Property='${ID}' Minimum='0.0.0000' />")
  xpStringIndentAppend(xml 4 "</Upgrade>")
  xpStringIndentAppend(xml 4 "<Condition Message='${prod} must be uninstalled prior to installation of this product.'>")
  xpStringIndentAppend(xml 5 "NOT ${ID}")
  xpStringIndentAppend(xml 4 "</Condition>")
  set(${appendTo} "${${appendTo}}${xml}" PARENT_SCOPE)
endfunction()

function(xpCpackWixUpgradeReplace appendTo guid ver4)
  set(xml "\n")
  # if ver4 looks like 1.2.3.10, ver3 would be 1.2.3
  string(REGEX REPLACE "([0-9]+)\\.([0-9]+)\\.([0-9]+)(\\.[0-9]+)?" "\\1.\\2.\\3" ver3 ${ver4})
  xpStringIndentAppend(xml 4 "<Upgrade Id='${guid}'>")
  # http://wix.tramontana.co.hu/tutorial/upgrades-and-modularization/replacing-ourselves
  xpStringIndentAppend(xml 5 "<UpgradeVersion OnlyDetect='no' Property='PREVIOUSFOUND' Minimum='0.0.0000' IncludeMinimum='yes'")
  xpStringIndentAppend(xml 6 "Maximum='${ver3}' IncludeMaximum='no' />")
  # http://windows-installer-xml-wix-toolset.687559.n2.nabble.com/Preventing-downgrades-as-a-launch-condition-tp4661741p4665418.html
  # TRICKY: msi sees 1.2.3.10 and 1.2.3.9 as 1.2.3 - they appear as the same
  # version. In order to prevent a downgrade on a 4th field change, rely on the
  # documented fact that msi ignores the 4th field to prevent installs where
  # the version numbers are identical to Windows Installer.
  xpStringIndentAppend(xml 5 "<UpgradeVersion OnlyDetect='yes' Property='ANOTHERBUILDINSTALLED' Minimum='${ver3}' Maximum='${ver3}'")
  xpStringIndentAppend(xml 6 "IncludeMinimum='yes' IncludeMaximum='yes' />")
  xpStringIndentAppend(xml 4 "</Upgrade>")
  xpStringIndentAppend(xml 4 "<CustomAction Id='BlockAnotherBuildInstall'")
  xpStringIndentAppend(xml 5 "Error='Another version of ${ver3} is already installed. Please uninstall ${ver3}.x before installing ${ver4}.' />")
  xpStringIndentAppend(xml 4 "<InstallExecuteSequence>")
  xpStringIndentAppend(xml 5 "<Custom Action='BlockAnotherBuildInstall' After='FindRelatedProducts'>")
  xpStringIndentAppend(xml 6 "<![CDATA[ANOTHERBUILDINSTALLED]]>")
  xpStringIndentAppend(xml 5 "</Custom>")
  xpStringIndentAppend(xml 4 "</InstallExecuteSequence>")
  set(${appendTo} "${${appendTo}}${xml}" PARENT_SCOPE)
endfunction()

function(xpCpackWixCAFeature appendTo)
  set(xml "\n")
  xpStringIndentAppend(xml 4 "<Feature Id='CAFeature' Title='Custom Actions' AllowAdvertise='no' Display='hidden' Level='1'>")
  xpStringIndentAppend(xml 5 "<ComponentGroupRef Id='CAGroup'/>")
  xpStringIndentAppend(xml 4 "</Feature>")
  set(${appendTo} "${${appendTo}}${xml}" PARENT_SCOPE)
endfunction()

function(xpCpackWixCASet id exe args when _act _seq) # optional ARGV6
  # CustomAction
  xpStringIndentAppend(act 2 "<CustomAction Id='${id}' Execute='deferred' Impersonate='no'")
  xpStringIndentAppend(act 3 "FileKey='CM_FP_${exe}' ExeCommand='${args}'")
  xpStringIndentAppend(act 2 "/>")
  set(${_act} "${${_act}}${act}" PARENT_SCOPE)
  # InstallExecuteSequence
  if(${ARGC} EQUAL 7 AND ARGV6)
    xpCpackWixCASeq(${id} ${exe} ${when} seq ${ARGV6})
  else()
    xpCpackWixCASeq(${id} ${exe} ${when} seq)
  endif()
  set(${_seq} "${${_seq}}${seq}" PARENT_SCOPE)
endfunction()

function(xpCpackWixCASetQt id exe args when _act _seq) # optional ARGV6
  # CustomAction
  xpStringIndentAppend(act 2 "<CustomAction Id='${id}Cmd' Property='${id}'")
  xpStringIndentAppend(act 3 "Value='&quot\;[#CM_FP_${exe}]&quot\; ${args}'")
  xpStringIndentAppend(act 2 "/>")
  xpStringIndentAppend(act 2 "<CustomAction Id='${id}' Execute='deferred' Impersonate='no'")
  xpStringIndentAppend(act 3 "BinaryKey='WixCA' DllEntry='CAQuietExec64'")
  xpStringIndentAppend(act 2 "/>")
  set(${_act} "${${_act}}${act}" PARENT_SCOPE)
  # InstallExecuteSequence
  if(${ARGC} EQUAL 7 AND ARGV6)
    xpCpackWixCASeq(${id}Cmd ${exe} ${when} seq1 ${ARGV6})
  else()
    xpCpackWixCASeq(${id}Cmd ${exe} ${when} seq1)
  endif()
  xpCpackWixCASeq(${id} ${exe} ${when} seq2 ${id}Cmd)
  set(${_seq} "${${_seq}}${seq1}${seq2}" PARENT_SCOPE)
endfunction()

function(xpCpackWixCASeq id exe when _seq) # optional ARGV4
  xpStringIndentAppend(seq 3 "<Custom Action='${id}'")
  if(${when} STREQUAL INS)
    set(before RegisterUser)
    set(condition "$CM_CP_${exe}&gt\;2")
  elseif(${when} STREQUAL SILENT_INS)
    set(before RegisterUser)
    # INSTALLUILEVEL_[NONE|BASIC|REDUCED|FULL] = [2|3|4|5]
    set(condition "$CM_CP_${exe}&gt\;2 AND UILevel&gt\;4")
  elseif(${when} STREQUAL UNINS)
    set(before UnpublishFeatures)
    set(condition "?CM_CP_${exe}=3 OR $CM_CP_${exe}=2")
  endif()
  if(${ARGC} EQUAL 5 AND ARGV4)
    xpStringIndentAppend(seq 4 "After='${ARGV4}'>")
  else()
    xpStringIndentAppend(seq 4 "Before='${before}'>")
  endif()
  xpStringIndentAppend(seq 4 "${condition}")
  xpStringIndentAppend(seq 3 "</Custom>")
  set(${_seq} "${seq}" PARENT_SCOPE)
endfunction()

function(xpCpackWixCAGroup filename) # optional ARGV1, ARGV2
  xpStringIndentAppend(xml 0 "<?xml version='1.0' encoding='UTF-8'?>")
  xpStringIndentAppend(xml 0 "<Wix xmlns='http://schemas.microsoft.com/wix/2006/wi'")
  xpStringIndentAppend(xml 2 "xmlns:util='http://schemas.microsoft.com/wix/UtilExtension'>")
  xpStringIndentAppend(xml 1 "<Fragment>")
  xpStringIndentAppend(xml 2 "<ComponentGroup Id='CAGroup'/>")
  if(ARGV1) # custom action xml
    set(xml "${xml}${ARGV1}")
  endif()
  if(ARGV2) # install execute sequence xml
    xpStringIndentAppend(xml 2 "<InstallExecuteSequence>")
    set(xml "${xml}${ARGV2}")
    xpStringIndentAppend(xml 2 "</InstallExecuteSequence>")
  endif()
  xpStringIndentAppend(xml 1 "</Fragment>")
  xpStringIndentAppend(xml 0 "</Wix>")
  file(WRITE ${filename} ${xml})
endfunction()
